plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.26.0'
}

def openjfxVersion = '20.0.1'
def osName = System.properties['os.name'].toLowerCase()
def arch = System.properties['os.arch'].toLowerCase()
def openjfxPlatform = osName.contains('mac') & arch.contains("aarch64") ? 'mac-aarch64' : osName.contains('mac') ? 'mac' : osName.contains('win') ? 'win' : osName.contains('linux') ? 'linux' : null

dependencies {
    implementation project(':domain')
    implementation(group: 'org.openjfx', name: 'javafx-base', version: openjfxVersion, classifier: openjfxPlatform)
    implementation(group: 'org.openjfx', name: 'javafx-graphics', version: openjfxVersion, classifier: openjfxPlatform)
    implementation(group: 'org.openjfx', name: 'javafx-media', version: openjfxVersion, classifier: openjfxPlatform)
    implementation(group: 'org.openjfx', name: 'javafx-controls', version: openjfxVersion, classifier: openjfxPlatform)
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.assertj:assertj-core')
}

application {
    mainModule = 'guesscharts.fx'
    mainClass = 'guesscharts.java_fx.Gui'
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    jpackage {
        imageName = "Guesscharts"
        installerName = "Guesscharts"
        imageOptions = ['--mac-package-identifier', 'guesscharts']
        def imgType = osName.contains('win') ? 'ico' : osName.contains('mac') ? 'icns' : 'png'
        icon = "src/main/resources/icon.$imgType"
    }
}

distributions {
    main {
        distributionBaseName = 'guesscharts'
    }
}

